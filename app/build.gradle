plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '2.1.0'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "swishticker-v${variant.versionName}.apk"
        }
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.hinterlong.kevin.swishticker"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 7
        versionName "1.1.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'basketballstats'
            keyAlias 'basketballstats'
            keyPassword 'basketballstats'
        }
        if (file('upload.keystore').exists()) {
            upload {
                storeFile file('upload.keystore')
                storePassword System.getenv('SWISHTICKER_UPLOAD_STORE_PASSWORD')
                keyAlias 'kevinandroidkey'
                keyPassword System.getenv('SWISHTICKER_UPLOAD_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            if (file('upload.keystore').exists()) {
                signingConfig signingConfigs.upload
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        textReport true
        textOutput 'stdout'
        lintConfig rootProject.file('lint.xml')
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha04'
    implementation "androidx.annotation:annotation:1.1.0-beta01"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //custom
    implementation "com.mikepenz:aboutlibraries:6.2.3"
    implementation "com.mikepenz:materialdrawer:6.1.2"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.mikepenz:iconics-core:3.2.2'
    implementation 'com.mikepenz:iconics-views:3.2.2'
    implementation 'com.mikepenz:google-material-typeface:3.0.1.3.original@aar'
    implementation 'com.mikepenz:fontawesome-typeface:5.3.1.1@aar'
    implementation 'eu.davidea:flexible-adapter:5.1.0'
    implementation 'eu.davidea:flexible-adapter-ui:1.0.0'
    implementation 'com.jakewharton:butterknife:10.1.0'
    kapt 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.0'

    //room

    def room_version = "2.1.0-alpha06"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha02'
}


play {
    serviceAccountCredentials = file("upload.json")
    track = "beta"
}
repositories {
    mavenCentral()
}
